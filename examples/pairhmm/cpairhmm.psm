%--------------------------------------------
% Constrained Pair HMM
% Christian Theil Have, Jan. 2010.
%--------------------------------------------

% Load the CHMM framework
:- ['../../chmm.pl'].

% Load constraints declared for this model
:- [constraints].

% List of symbols which may be used in sequences aligned.
% You will need change this to the alphabet of your sequences.
alphabet([a,c,g,t]).

% MSW declarations:

% State transition MSWs
values(trans(begin),[match,delete,insert]).
values(trans(match), [match,delete,insert]).
values(trans(insert), [match,insert]).
values(trans(delete), [match,delete]).

% Emission MSWs are declared as possible emit pattern permutations for each state.
values(emit(State), ABSet) :-
	alphabet(A),
	findall(AB, (member(X,A),member(Y,A),emit_pattern(State,X,Y,AB)), ABList),
	eliminate_duplicate(ABList, ABSet).

emit_pattern(match, A, B, [A,B]).
emit_pattern(delete, _, B, [-,B]).
emit_pattern(insert, A, _, [A,-]).

cpairhmm(InputA,InputB,--Alignment) :-
	init_global_constraint_store,
	cpairhmm(begin,InputA,InputB,--Alignment).

cpairhmm(PreviousState,[],[B|BRest],--[Emit|AlignmentRest]) :-
	msw(trans(PreviousState), delete),
	msw(emit(delete), Emit),
	emit_pattern(delete,-,B,Emit),
	check_constraints([delete,Emit]),
	cpairhmm(delete,[],BRest,--AlignmentRest).

cpairhmm(PreviousState,[A|ARest],[],--[Emit|AlignmentRest]) :-
	msw(trans(PreviousState), insert),
	msw(emit(insert), Emit),
	emit_pattern(insert,A,-,Emit),
	check_constraints([insert,Emit]),
	cpairhmm(insert,ARest,[],--AlignmentRest).

cpairhmm(PreviousState,[A|ARest],[B|BRest],--[Emit|AlignmentRest]) :-
	msw(trans(PreviousState), NextState),
	msw(emit(NextState),Emit),
	emit_pattern(NextState,A,B,Emit),
	check_constraints([NextState,Emit]),
	((NextState==match,
	  cpairhmm(NextState,ARest,BRest,--AlignmentRest))
	;
	 ((NextState==insert,
	  cpairhmm(NextState,ARest,[B|BRest],--AlignmentRest)))
	;
	 ((NextState==delete,
	  cpairhmm(NextState,[A|ARest],BRest,--AlignmentRest)))).

cpairhmm(_,[],[],--[]).

