%--------------------------------------------
% Constrained Pair HMM
% Christian Theil Have, Jan. 2010.
%--------------------------------------------
:- [constraint_integration].
:- [constraint_checkers].
:- [constraints].

% Use for debugging:
cmsw(A,B) :- values(A,AVals), member(B,AVals).
%#cmsw(A,B) :- msw(A,B).

% List of symbols which may be used in sequences aligned.
%alphabet([a,c,g,t]).
alphabet([1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20]).

match_permutations(A,P) :-
	findall([X,Y], (member(X,A),member(Y,A)), P).

% MSW declarations

values(emit(insert), ABList) :-
	findall(AB,(alphabet(A),member(E,A),emit(insert,E,-,AB)),ABList).

values(emit(delete), ABList) :-
	findall(AB,(alphabet(B),member(E,B),emit(delete,-,E,AB)),ABList).

values(emit(match), AB) :-
	alphabet(A),
	match_permutations(A,AB).

values(trans(begin),[match,delete,insert]).
values(trans(match), [match,delete,insert]).
values(trans(insert), [match,insert]).
values(trans(delete), [match,delete]).

emit(match, A, B, [A,B]).
emit(delete, _, B, [-,B]).
emit(insert, A, _, [A,-]).

cpairhmm(InputA,InputB,Alignment) :-
	init_local_constraint_store(ConstraintStore),
	cpairhmm(begin,InputA,InputB,ConstraintStore,Alignment).

cpairhmm(PreviousState,[],[B|BRest], ConstraintsBefore,[Emit|AlignmentRest]) :-
	cmsw(trans(PreviousState), delete),
	cmsw(emit(delete), Emit),
	emit(delete,-,B,Emit),
	check_constraints([delete,Emit],ConstraintsBefore,ConstraintsAfter),
	cpairhmm(delete,[],BRest,ConstraintsAfter,AlignmentRest).

cpairhmm(PreviousState,[A|ARest],[], ConstraintsBefore,[Emit|AlignmentRest]) :-
	cmsw(trans(PreviousState), insert),
	cmsw(emit(insert), Emit),
	emit(insert,A,-,Emit),
	check_constraints([insert,Emit],ConstraintsBefore,ConstraintsAfter),
	cpairhmm(insert,ARest,[],ConstraintsAfter,AlignmentRest).

cpairhmm(PreviousState,[A|ARest],[B|BRest], ConstraintsBefore,[Emit|AlignmentRest]) :-
	cmsw(trans(PreviousState), NextState),
	cmsw(emit(NextState),Emit),
	emit(NextState,A,B,Emit),
	check_constraints([NextState,Emit],ConstraintsBefore,ConstraintsAfter),
	((NextState==match,
	  cpairhmm(NextState,ARest,BRest,ConstraintsAfter,AlignmentRest))
	;
	 ((NextState==insert,
	  cpairhmm(NextState,ARest,[B|BRest],ConstraintsAfter,AlignmentRest)))
	;
	 ((NextState==delete,
	  cpairhmm(NextState,[A|ARest],BRest,ConstraintsAfter,AlignmentRest)))).

cpairhmm(_,[],[],_,[]).

